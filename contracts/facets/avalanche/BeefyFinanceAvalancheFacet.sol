// SPDX-License-Identifier: BUSL-1.1
// Last deployed from commit: ;
pragma solidity ^0.8.4;

import "@openzeppelin/contracts/token/ERC20/extensions/IERC20Metadata.sol";
import "../../ReentrancyGuardKeccak.sol";
import "@uniswap/lib/contracts/libraries/TransferHelper.sol";
import "redstone-evm-connector/lib/contracts/commons/ProxyConnector.sol";
import "../../lib/SolvencyMethods.sol";
import "../../interfaces/facets/avalanche/IBeefyFinance.sol";

import {DiamondStorageLib} from "../../lib/DiamondStorageLib.sol";

//This path is updated during deployment
import "../../lib/local/DeploymentConstants.sol";
import "../../interfaces/facets/avalanche/IBeefyFinance.sol";

contract BeefyFinanceAvalancheFacet is ReentrancyGuardKeccak, SolvencyMethods {
    using TransferHelper for address payable;
    using TransferHelper for address;

    // Vaults
    address private constant MOO_PNG_USDC_AVAX_LP = 0xf3340EdF16563D52C7E7C576F2fCC8f3D52464aB;
    address private constant MOO_PNG_USDCe_AVAX_LP = 0x9B02209a331c072637C6eBd34cdCD6b6A16987a9;
    address private constant MOO_TJ_USDC_AVAX_LP = 0x7E5bC7088aB3Da3e7fa1Aa7ceF1dC73F5B00681c;

    // LPs
    address private constant PNG_USDC_AVAX_LP = 0x0e0100Ab771E9288e0Aa97e11557E6654C3a9665;
    address private constant PNG_USDCe_AVAX_LP = 0xbd918Ed441767fe7924e99F6a0E0B568ac1970D9;
    address private constant TJ_USDC_AVAX_LP = 0xf4003F4efBE8691B60249E6afbD307aBE7758adb;

    // ----- STAKE -----

    /**
      * Stakes PNG_USDC_AVAX_LP in the Beefy protocol
      * @dev This function uses the redstone-evm-connector
      * @param amount amount of PNG_USDC_AVAX_LP to be staked
    **/
    function stakePngUsdcAvaxLpBeefy(uint256 amount) public onlyOwner nonReentrant remainsSolvent {
        _stakeLpBeefy(IBeefyFinance.BeefyStakingDetails({
            lpTokenAddress: PNG_USDC_AVAX_LP,
            vaultAddress: MOO_PNG_USDC_AVAX_LP,
            lpTokenSymbol: "PNG_USDC_AVAX_LP",
            vaultTokenSymbol: "MOO_PNG_USDC_AVAX_LP",
            amount: amount
        }));
    }

    /**
      * Stakes PNG_USDCe_AVAX_LP in the Beefy protocol
      * @dev This function uses the redstone-evm-connector
      * @param amount amount of PNG_USDCe_AVAX_LP to be staked
    **/
    function stakePngUsdceAvaxLpBeefy(uint256 amount) public onlyOwner nonReentrant remainsSolvent {
        _stakeLpBeefy(IBeefyFinance.BeefyStakingDetails({
        lpTokenAddress: PNG_USDCe_AVAX_LP,
        vaultAddress: MOO_PNG_USDCe_AVAX_LP,
        lpTokenSymbol: "PNG_USDCe_AVAX_LP",
        vaultTokenSymbol: "MOO_PNG_USDCe_AVAX_LP",
        amount: amount
        }));
    }

    /**
      * Stakes TJ_USDC_AVAX_LP in the Beefy protocol
      * @dev This function uses the redstone-evm-connector
      * @param amount amount of TJ_USDC_AVAX_LP to be staked
    **/
    function stakeTjUsdcAvaxLpBeefy(uint256 amount) public onlyOwner nonReentrant remainsSolvent {
        _stakeLpBeefy(IBeefyFinance.BeefyStakingDetails({
        lpTokenAddress: TJ_USDC_AVAX_LP,
        vaultAddress: MOO_TJ_USDC_AVAX_LP,
        lpTokenSymbol: "TJ_USDC_AVAX_LP",
        vaultTokenSymbol: "MOO_TJ_USDC_AVAX_LP",
        amount: amount
        }));
    }

    // ----- UNSTAKE -----

    /**
      * Unstakes PNG_USDC_AVAX_LP from the Beefy protocol
      * @dev This function uses the redstone-evm-connector
      * @param amount amount of PNG_USDC_AVAX_LP to be unstaked
    **/
    function unstakePngUsdcAvaxLpBeefy(uint256 amount) public onlyOwner nonReentrant remainsSolvent {
        _unstakeLpBeefy(IBeefyFinance.BeefyStakingDetails({
        lpTokenAddress: PNG_USDC_AVAX_LP,
        vaultAddress: MOO_PNG_USDC_AVAX_LP,
        lpTokenSymbol: "PNG_USDC_AVAX_LP",
        vaultTokenSymbol: "MOO_PNG_USDC_AVAX_LP",
        amount: amount
        }));
    }

    /**
      * Unstakes PNG_USDCe_AVAX_LP from the Beefy protocol
      * @dev This function uses the redstone-evm-connector
      * @param amount amount of PNG_USDCe_AVAX_LP to be unstaked
    **/
    function unstakePngUsdceAvaxLpBeefy(uint256 amount) public onlyOwner nonReentrant remainsSolvent {
        _unstakeLpBeefy(IBeefyFinance.BeefyStakingDetails({
        lpTokenAddress: PNG_USDCe_AVAX_LP,
        vaultAddress: MOO_PNG_USDCe_AVAX_LP,
        lpTokenSymbol: "PNG_USDCe_AVAX_LP",
        vaultTokenSymbol: "MOO_PNG_USDCe_AVAX_LP",
        amount: amount
        }));
    }

    /**
      * Untakes TJ_USDC_AVAX_LP from the Beefy protocol
      * @dev This function uses the redstone-evm-connector
      * @param amount amount of TJ_USDC_AVAX_LP to be unstaked
    **/
    function unstakeTjUsdcAvaxLpBeefy(uint256 amount) public onlyOwner nonReentrant remainsSolvent {
        _unstakeLpBeefy(IBeefyFinance.BeefyStakingDetails({
        lpTokenAddress: TJ_USDC_AVAX_LP,
        vaultAddress: MOO_TJ_USDC_AVAX_LP,
        lpTokenSymbol: "TJ_USDC_AVAX_LP",
        vaultTokenSymbol: "MOO_TJ_USDC_AVAX_LP",
        amount: amount
        }));
    }

    // ----- PRIVATE METHODS -----


    /**
      * Stakes {stakingDetails.lpTokenAddress} LP token in the Beefy protocol
      * @dev This function uses the redstone-evm-connector
      * @param stakingDetails IBeefyFinance.BeefyStakingDetails staking details
    **/
    function _stakeLpBeefy(IBeefyFinance.BeefyStakingDetails memory stakingDetails) private {
        TokenManager tokenManager = DeploymentConstants.getTokenManager();

        require(stakingDetails.amount > 0, "Cannot stake 0 tokens");
        // _ACTIVE = 2
        require(tokenManager.tokenToStatus(stakingDetails.lpTokenAddress) == 2, "LP token not supported");
        require(tokenManager.tokenToStatus(stakingDetails.vaultAddress) == 2, "Vault token not supported");
        require(IERC20(stakingDetails.lpTokenAddress).balanceOf(address(this)) >= stakingDetails.amount, "Not enough LP token available");

        IERC20Metadata(stakingDetails.lpTokenAddress).approve(stakingDetails.vaultAddress, stakingDetails.amount);
        IBeefyFinance(stakingDetails.vaultAddress).deposit(stakingDetails.amount);

        // Add/remove owned tokens
        DiamondStorageLib.addOwnedAsset(stakingDetails.vaultTokenSymbol, stakingDetails.vaultAddress);
        if(IERC20(stakingDetails.lpTokenAddress).balanceOf(address(this)) == 0) {
            DiamondStorageLib.removeOwnedAsset(stakingDetails.lpTokenSymbol);
        }

        emit Staked(msg.sender, stakingDetails.lpTokenSymbol, stakingDetails.amount, block.timestamp);
    }

    /**
              * Unstakes {stakingDetails.lpTokenAddress} LP token in the Beefy protocol
              * @dev This function uses the redstone-evm-connector
              * @param stakingDetails IBeefyFinance.BeefyStakingDetails staking details
    **/
    function _unstakeLpBeefy(IBeefyFinance.BeefyStakingDetails memory stakingDetails) private {
        IBeefyFinance vaultContract = IBeefyFinance(stakingDetails.vaultAddress);
        uint256 initialStakedBalance = vaultContract.balanceOf(address(this));

        require(initialStakedBalance >= stakingDetails.amount, "Cannot unstake more than was initially staked");

        vaultContract.withdraw(stakingDetails.amount);

        // Add/remove owned tokens
        DiamondStorageLib.addOwnedAsset(stakingDetails.lpTokenSymbol, stakingDetails.lpTokenAddress);
        if(vaultContract.balanceOf(address(this)) == 0) {
            DiamondStorageLib.removeOwnedAsset(stakingDetails.vaultTokenSymbol);
        }

        emit Unstaked(msg.sender, stakingDetails.lpTokenSymbol, stakingDetails.amount, block.timestamp);
    }


    modifier onlyOwner() {
        DiamondStorageLib.enforceIsContractOwner();
        _;
    }

    /* ========== RECEIVE AVAX FUNCTION ========== */
    receive() external payable {}

    /**
        * @dev emitted when user stakes an asset
        * @param user the address executing staking
        * @param asset the asset that was staked
        * @param amount of the asset that was staked
        * @param timestamp of staking
    **/
    event Staked(address indexed user, bytes32 indexed asset, uint256 amount, uint256 timestamp);

    /**
        * @dev emitted when user unstakes an asset
        * @param user the address executing unstaking
        * @param asset the asset that was unstaked
        * @param amount of the asset that was unstaked
        * @param timestamp of unstaking
    **/
    event Unstaked(address indexed user, bytes32 indexed asset, uint256 amount, uint256 timestamp);
}